plugins {
    id "com.avast.gradle.docker-compose" version "0.6.6"
}

group 'io.gitsocratic'
version '0.2.1'

apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = 'io.gitsocratic.SocraticCLI'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://repo.grakn.ai/repository/maven/'
    }
}

ext {
    graknVersion = "1.7.2"
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'com.github.sourceplusplus.assistant:source-api:master-SNAPSHOT'

    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
    implementation 'org.codehaus.groovy:groovy-all:2.4.17'
    implementation group: 'info.picocli', name: 'picocli', version: '4.2.0'
    implementation group: 'org.codehaus.gpars', name: 'gpars', version: '1.2.1'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.7.0.202003110725-r'
    implementation "com.github.rholder:guava-retrying:2.0.0"
    implementation group: 'com.github.docker-java', name: 'docker-java', version: '3.2.1'
    compile(group: 'com.github.codebrig', name: 'phenomena', version: '6441ef3aa7') {
        exclude group: 'io.grakn.client', module: 'api'
    }
    implementation 'com.github.codebrig:arthur:4c66a0b7ec'
    implementation group: 'io.grakn.client', name: 'grakn-client', version: "$graknVersion"
    implementation group: 'io.graql', name: 'graql-lang', version: "1.0.6"
    compile group: 'io.vertx', name: 'vertx-core', version: '3.8.5'
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.8'
}

configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'
}

test {
    dependsOn "composeUp"
    finalizedBy "composeDown"

    testLogging {
        events "passed", "skipped", "failed"
        setExceptionFormat("full")

        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

dockerCompose {
    useComposeFiles = ['./docker-compose.yml']
    dockerComposeWorkingDirectory = './docker/'
    captureContainersOutput = true
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/gitsocratic_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['grakn_version'] = "$graknVersion".toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}
