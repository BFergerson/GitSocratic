plugins {
    id "com.avast.gradle.docker-compose" version "0.6.6"
}

group 'io.gitsocratic'
version '0.2'

apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = 'io.gitsocratic.SocraticCLI'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://repo.grakn.ai/repository/maven/'
    }
}

ext {
    babelfishVersion = '1.11.0'
    graknVersion = "1.5.7"
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.25'
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    implementation group: 'info.picocli', name: 'picocli', version: '3.7.0'
    implementation group: 'org.codehaus.gpars', name: 'gpars', version: '1.2.1'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.0.1.201806211838-r'
    implementation "com.github.rholder:guava-retrying:2.0.0"
    implementation group: 'com.github.docker-java', name: 'docker-java', version: '3.1.0-rc-5'
    implementation 'com.github.codebrig:phenomena:ad0111531f'
    implementation 'com.github.codebrig:arthur:e10b35a5c8'
    implementation "org.bblfsh:bblfsh-client:$babelfishVersion"
    //implementation group: 'io.grakn.client', name: 'api', version: "$graknVersion"
    implementation 'com.github.sourceplusplus.assistant:source-api:v0.2.1-alpha'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.7.0'

    //todo: remove
    compile files('lib/io-grakn-client-grakn-client-1.5.7.jar')
    compile files('lib/io-grakn-core-grakn-api-1.5.7.jar')
    compile files('lib/io-grakn-protocol-grakn-protocol-1.5.7.jar')
    compile files('lib/io-graql-graql-grammar-1.5.7.jar')
    compile files('lib/io-graql-graql-lang-1.5.7.jar')
}

configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'
}

test {
    dependsOn "composeUp"
    finalizedBy "composeDown"
}

dockerCompose {
    useComposeFiles = ['./docker-compose.yml']
    dockerComposeWorkingDirectory = './docker/'
    captureContainersOutput = true
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/gitsocratic_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['babelfish_version'] = "$babelfishVersion".toString()
            p['grakn_version'] = "$graknVersion".toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}